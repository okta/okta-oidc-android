// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        mavenCentral()
        google()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.2.2'
        classpath 'org.owasp:dependency-check-gradle:5.3.2'
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.22.0'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    group = project.property('GROUP')
    version = project.property('VERSION_NAME')

    repositories {
        // Insert local test repo here
        google()
    }

    configurePublishing(it)
}

def configurePublishing(Project project) {
    project.plugins.withId('com.vanniktech.maven.publish.base') {
        project.apply plugin: 'signing'

        project.mavenPublishing {
            publishToMavenCentral("DEFAULT")
            pomFromGradleProperties()
        }

        if (project.hasProperty('signWithGpgCommand')) {
            project.afterEvaluate {
                project.signing {
                    useGpgCmd()
                    sign project.publishing.publications
                }
            }
        }

        if (project.hasProperty('signAllPublications')) {
            project.mavenPublishing {
                signAllPublications()
            }
        }

        if (project.plugins.hasPlugin('com.android.library')) {
            project.mavenPublishing.configure(new com.vanniktech.maven.publish.AndroidSingleVariantLibrary("release"))
        }
    }
}

subprojects {
    repositories {
        mavenCentral()
        maven { url "https://maven.google.com" }
        flatDir {
            dirs 'libs'
        }
    }
}

ext {
    minSdkVersion = 21
    compileSdkVersion = 33
    browserVersion = '1.4.0'
    annotationVersion = '1.3.0'
    appcompatVersion = '1.4.2'
    androidxCore = '1.9.0'
    // SDK dependency versions
    gsonVersion = "2.9.1"

    // Test dependency versions
    junitVersion = '4.13.2'
    mockitoVersion = "4.6.1"
    robolectricVersion = '4.8.1'
    okhttpVersion = '4.10.0'
    jsonWebTokenVersion = '0.11.2'
    assertjCoreVersion = '3.16.1'

    androidxLibVersion = '1.5.3'
    espressoVersion = "3.4.0"
    uiautomatorVersion = "2.2.0"
    securityVersion = "1.0.0"

    // Gradle plugin version
    checkstyleVersion = "10.3"
}

task showVersion {
    doLast {
        logger.lifecycle("Version ID: " + project.versionNum)
        logger.lifecycle("Version Name: " + project.versionName)
        logger.lifecycle("Version Date: " + project.versionDate)
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply plugin: "com.android.application"
apply from: "${rootDir}/gradle/style.gradle"
apply from: "${rootDir}/gradle/okta-common.gradle"

android {
    defaultConfig {
        minSdkVersion 23
        compileSdkVersion rootProject.ext.compileSdkVersion
        targetSdkVersion rootProject.compileSdkVersion
        applicationId "com.okta.oidc.example"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        Properties properties = new Properties()
        if (project.rootProject.file('local.properties').exists()) {
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
        }
        Boolean travis = System.getenv("TRAVIS") == "true"
        String manifestScheme = properties.getProperty('test.scheme', '""')
        buildConfigField "String", "USERNAME_MFA", travis ? '""' : properties.getProperty('mfa.username', '""')
        buildConfigField "String", "PASSWORD_MFA", travis ? '""' : properties.getProperty('mfa.password', '""')
        buildConfigField "String", "USERNAME", travis ? '""' : properties.getProperty('test.username', '""')
        buildConfigField "String", "PASSWORD", travis ? '""' : properties.getProperty('test.password', '""')
        buildConfigField "String", "PINCODE", travis ? '""' : properties.getProperty('test.pincode', '""')
        buildConfigField "String", "CLIENT_ID", travis ? '""' : properties.getProperty('test.clientId', '""')
        buildConfigField "String", "REDIRECT_URI", travis ? '""' : properties.getProperty('test.redirectUri', '""')
        buildConfigField "String", "END_SESSION_URI", travis ? '""' : properties.getProperty('test.endSessionUri', '""')
        buildConfigField "String[]", "SCOPES", travis ? "{}" : ("{" + properties.getProperty('test.scopes', '""') + "}")
        buildConfigField "String", "DISCOVERY_URI", travis ? '""' : properties.getProperty('test.discoveryUri', '""')
        buildConfigField "String[]", "IDP_SCOPE", travis ? "{}" : ("{" + properties.getProperty('test.idp_scope', '""') + "}")
        buildConfigField "String", "IDP", travis ? '""' : properties.getProperty('test.idp', '""')
        buildConfigField "String", "IDP_USERNAME", travis ? '""' : properties.getProperty('test.idp_username', '""')
        buildConfigField "String", "IDP_PASSWORD", travis ? '""' : properties.getProperty('test.idp_password', '""')

        // Make sure this is consistent with the redirect URI used in
        // res/raw/okta_oidc_config
        manifestPlaceholders = [
                "appAuthRedirectScheme": manifestScheme
        ]
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES']
            pickFirsts += ['META-INF/okta/version.properties']
        }
    }
    namespace 'com.okta.oidc.example'
}

dependencies {
    implementation project(':okta-oidc-android')
    implementation 'androidx.fragment:fragment:' + rootProject.androidxLibVersion
    implementation 'androidx.annotation:annotation:' + rootProject.annotationVersion
    implementation 'androidx.appcompat:appcompat:' + rootProject.appcompatVersion
    implementation 'androidx.security:security-crypto:' + rootProject.securityVersion

    implementation "com.google.android.material:material:1.7.0"
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'com.okta.authn.sdk:okta-authn-sdk-api:2.0.11'
    implementation('com.okta.authn.sdk:okta-authn-sdk-impl:2.0.11') {
        exclude group: 'com.okta.sdk', module: 'okta-sdk-httpclient'
    }

    implementation 'org.bouncycastle:bcprov-jdk18on:1.78.1'
    implementation 'org.yaml:snakeyaml:2.0'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.3'
    implementation 'com.okta.sdk:okta-sdk-okhttp:8.2.2'
    implementation "com.squareup.okhttp3:okhttp:${rootProject.okhttpVersion}"
    implementation "com.squareup.okio:okio:${rootProject.okioVersion}"
    implementation "com.squareup.okio:okio-jvm:${rootProject.okioVersion}"
    androidTestImplementation "androidx.browser:browser:${rootProject.browserVersion}"

    // Core library
    androidTestImplementation 'androidx.test:core:1.5.0'

    // Assertions
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.ext:truth:1.5.0'
    androidTestImplementation "com.google.truth:truth:${rootProject.truthVersion}"

    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    // Espresso dependencies
    androidTestImplementation 'androidx.test.espresso:espresso-core:' + rootProject.espressoVersion
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:' + rootProject.espressoVersion
    androidTestImplementation 'androidx.test.espresso:espresso-intents:' + rootProject.espressoVersion
    androidTestImplementation 'androidx.test.espresso:espresso-accessibility:' + rootProject.espressoVersion
    androidTestImplementation 'androidx.test.espresso:espresso-web:' + rootProject.espressoVersion
    androidTestImplementation 'androidx.test.espresso.idling:idling-concurrent:' + rootProject.espressoVersion

    // UiAutomator Testing
    androidTestImplementation 'androidx.test.uiautomator:uiautomator:' + rootProject.uiautomatorVersion
    androidTestImplementation 'org.hamcrest:hamcrest-integration:1.3'

    androidTestImplementation 'com.github.tomakehurst:wiremock-standalone:2.27.2'
}

def adb = System.getenv("ANDROID_HOME") + "/platform-tools/adb"
tasks.register("prepareDeviceForUITesting", Exec) {
    //disable animations for testing
    commandLine adb, 'shell', 'settings', 'put', 'global', 'window_animation_scale', '0'
    commandLine adb, 'shell', 'settings', 'put', 'global', 'transition_animation_scale', '0'
    commandLine adb, 'shell', 'settings', 'put', 'global', 'animator_duration_scale', '0'
    //always finish activity
    commandLine adb, 'shell', 'settings', 'put', 'global', 'always_finish_activities', '1'
    // push keystore file to sdcard
    commandLine adb, 'push', 'src/androidTest/assets/mock.keystore.bks', '/sdcard/Download/'
}

tasks.register("clearData", Exec) {
    commandLine adb, 'shell', 'pm', 'clear', 'com.okta.oidc.example'
    commandLine adb, 'shell', 'pm', 'clear', 'com.okta.oidc.example.test'
    commandLine adb, 'shell', 'pm', 'clear', 'com.android.chrome'
}

tasks.register("restoreDeviceSettings", Exec) {
    //enable animations for testing
    commandLine adb, 'shell', 'settings', 'put', 'global', 'window_animation_scale', '1'
    commandLine adb, 'shell', 'settings', 'put', 'global', 'transition_animation_scale', '1'
    commandLine adb, 'shell', 'settings', 'put', 'global', 'animator_duration_scale', '1'
    //disable finish activity
    commandLine adb, 'shell', 'settings', 'put', 'global', 'always_finish_activities', '0'
    // remove keystore file to sdcard
    commandLine adb, 'shell', 'rm', '/sdcard/Download/mock.keystore.bks'
}

dependencyCheck {
    suppressionFile file("../dependency-suppression.xml").toString()
}

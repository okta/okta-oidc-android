apply plugin: "com.android.library"
apply from: "${rootDir}/gradle/okta-common.gradle"
apply from: "${rootDir}/gradle/style.gradle"
apply plugin: "com.vanniktech.maven.publish.base"

android {
    defaultConfig {
        compileSdkVersion rootProject.ext.compileSdkVersion
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.compileSdkVersion
        archivesBaseName = "oidc-androidx"
        consumerProguardFiles 'proguard-rules.pro'

        buildConfigField "String", "VERSION_NAME", "\"${property('VERSION_NAME')}\""
    }

    lintOptions {
        warningsAsErrors true
        abortOnError true
        disable 'TrulyRandom', 'GradleDependency'
        baseline file("lint-baseline.xml")
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    sourceSets {
        main {
            java {
                srcDir "src/main/java"
            }
        }
        test {
            manifest {
                srcFile "${rootDir.absolutePath}/library/src/test/AndroidManifest.xml"
            }
        }
        androidTest {
            manifest {
                srcFile "${rootDir.absolutePath}/library/src/test/AndroidManifest.xml"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation "com.google.code.gson:gson:${rootProject.gsonVersion}"
    implementation "androidx.browser:browser:${rootProject.browserVersion}"
    implementation "androidx.fragment:fragment:${rootProject.androidxLibVersion}"
    implementation "androidx.core:core:${rootProject.androidxCore}"
    testImplementation "com.squareup.okhttp3:okhttp-tls:${rootProject.okhttpVersion}"
    testImplementation "junit:junit:${rootProject.junitVersion}"
    testImplementation "org.mockito:mockito-core:${rootProject.mockitoVersion}"
    testImplementation "org.mockito:mockito-inline:${rootProject.mockitoVersion}"
    testImplementation "org.robolectric:robolectric:${rootProject.robolectricVersion}"
    testImplementation "com.squareup.okhttp3:mockwebserver:${rootProject.okhttpVersion}"
    testImplementation "com.squareup.okhttp3:okhttp:${rootProject.okhttpVersion}"
    testImplementation "org.assertj:assertj-core:${rootProject.assertjCoreVersion}"
    testImplementation "io.jsonwebtoken:jjwt-api:${rootProject.jsonWebTokenVersion}"
    testImplementation "io.jsonwebtoken:jjwt-impl:${rootProject.jsonWebTokenVersion}"
    testImplementation("io.jsonwebtoken:jjwt-orgjson:${rootProject.jsonWebTokenVersion}") {
        exclude group: 'org.json', module: 'json' //provided by Android natively
    }
}

dependencyCheck {
    suppressionFile file("../dependency-suppression.xml").toString()
}
